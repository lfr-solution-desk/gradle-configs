project.ext['liferay.gpr.consuming.repos.names'] = 'static-repo-1, static-repo-2, f-another-static-feature-repo'

project.ext['liferay.gpr.consuming.actor'] = 'some_user'
project.ext['liferay.gpr.consuming.token'] = 'iuwerj2@$%adfaf44'

// Apply the plugin - a local script file
apply from: "${findProperty('tested.gradle.scripts.local.src.dir')}/gpr-consuming.gradle"


// traditional
dependencies {
    // the usual pattern of the groupId -- nothing else after the repo's name (f-root-feature-repo)
    compileOnly group: 'com.liferay.soldesk.f-root-feature-repo',                 name: 'some-artifact', version: '1.0.3'

    // unusual, but still allowed groupId -- there is some suffix after the repo's name (f-root-feature-repo-2)
    providedModules group: 'com.liferay.soldesk.f-root-feature-repo-2.modules.forms', name: 'some-artifact', version: '1.0.3'

    compileOnly group: 'com.liferay.soldesk.f-another-static-feature-repo',       name: 'some-artifact', version: '1.0.3'

    // make sure inferring does not crash when there is no "group"
    compileOnly files("lib/test.jar")
}


task runGradleTest {
    dependsOn assemble

    doLast {
        println rootProject.repositories.collect { it.name }

        // test url + creds are set correctly
        rootProject.repositories.getByName('GitHub Packages - lfr-solution-desk/static-repo-1').with { ArtifactRepository ar ->
            assert ar.url.toString() == 'https://maven.pkg.github.com/lfr-solution-desk/static-repo-1'
            assert ar.credentials.username == 'some_user'
            assert ar.credentials.password == 'iuwerj2@$%adfaf44'
        }

        // static
        assert rootProject.repositories.getByName('GitHub Packages - lfr-solution-desk/static-repo-1')
        assert rootProject.repositories.getByName('GitHub Packages - lfr-solution-desk/static-repo-2')
        assert rootProject.repositories.getByName('GitHub Packages - lfr-solution-desk/f-another-static-feature-repo')

        // detected based dependencies of the rootProject in this file's build.gradle
        assert rootProject.repositories.getByName('GitHub Packages - lfr-solution-desk/f-root-feature-repo')
        assert rootProject.repositories.getByName('GitHub Packages - lfr-solution-desk/f-root-feature-repo-2')
        assert rootProject.repositories.getByName('GitHub Packages - lfr-solution-desk/f-another-static-feature-repo')
        
        project(':modules:forms:form-api').with { module ->
            // static
            assert module.repositories.getByName('GitHub Packages - lfr-solution-desk/static-repo-1')
            assert module.repositories.getByName('GitHub Packages - lfr-solution-desk/static-repo-2')
            assert module.repositories.getByName('GitHub Packages - lfr-solution-desk/f-another-static-feature-repo')

            // detected based on declarations in build.gradle of the module
            assert module.repositories.getByName('GitHub Packages - lfr-solution-desk/f-module-feature-repo')
        }
    }
}